Most.js source for a `map` function in a view
---------

Maps results from a changes stream into a view

The view is evaluated locally (since CouchDB does not support `changes` on views).

The `map_function` must actually be a wrapper for the map function, it is called with `emit` as its parameter.
The `db_changes` stream is generated by e.g. `all_changes`.

    changes_view = (map_function,db_changes,{include_docs} = {}) ->
      db_changes
      .chain ({id,seq,deleted,doc}) ->

        create (add,end,error) ->

Emulate a row, similarly to what `view_stream` does.

          emit = (key,value) ->
            content = {id,seq,deleted,key,value}
            content.doc = doc if include_docs
            add content

          fn = map_function emit

          try
            fn Object.assign {}, doc
          catch e
            error e
          end()
          return

    {create} = require '@most/create'
    module.exports = changes_view
