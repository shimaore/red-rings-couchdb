View dispatcher
---------------

    view_as_stream = (db_uri,app,view,key) ->

For queries we need to keep the original `key` field (for routing purposes) while we set the `_key` field to the key generated by the view.

      response = (msg) ->
        reduced_key = msg.get 'key'
        msg
        .set 'key', key
        .set '_key', reduced_key

The `key` field (in the `SUBSCRIBE` message) is used to determine what type of query is performed:

      switch

- Map (JSON object) is used to provide one-time queries. These do not support `changes` streaming.

        when Immutable.Map.isMap key
          switch
            when key.has 'count'
              count_as_stream db_uri,app,view,key.get('count'),key.get('detail')
              .map response

            when key.has('min') or key.has('max')
              range_as_stream db_uri,app,view,key.get('min'),key.get('max')
              .map response

- string, List, etc., generate regular view queries (and are subject to `changes` streaming, see `./changes-view`).

        else
          return query_as_stream db_uri,app,view,key

    module.exports = view_as_stream
    query_as_stream = require './query'
    count_as_stream = require './count'
    range_as_stream = require './range'
    Immutable = require 'immutable'
